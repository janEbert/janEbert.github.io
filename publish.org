#+begin_src emacs-lisp :exports results :results none :var project-setup=setup-project :tangle no
;; Automatically defines the setup function below.
(require 'ox-publish)

(org-publish-project "personal-website")
;; Force re-generating all files
;; Removing timestamps is especially important to get sitemap titles up to date.
;; (org-publish-remove-all-timestamps)
;; This is already called when removing timestamps; we do it again for
;; forward compatibility.
;; (org-publish-reset-cache)
;; (org-publish-project "personal-website" t)
#+end_src

# Append project to global project-alist.
#+begin_src emacs-lisp :results none :var project-setup=setup-project :tangle no
(setq-default org-publish-project-alist
			  (append (default-value 'org-publish-project-alist)
					  (buffer-local-value
					   'org-publish-project-alist (current-buffer))))
#+end_src

# Set project as global project-alist.
#+begin_src emacs-lisp :results none :var project-setup=setup-project :tangle no
(setq-default org-publish-project-alist
			  (buffer-local-value
			   'org-publish-project-alist (current-buffer)))
#+end_src

# Add project as directory variable.
#+begin_src emacs-lisp :results none :var project-setup=setup-project :tangle no
(add-dir-local-variable 'org-mode 'org-publish-project-alist
						(buffer-local-value
						 'org-publish-project-alist (current-buffer)))
#+end_src

#+name: setup-project
#+begin_src emacs-lisp :exports results :results none :tangle yes
(require 'ox-publish)

(defun jan-website--setup-project ()
  "Setup the project's publishing environment.
The modifications must be pure so no matter how often the function is run,
the environment is the same."
  (let* ((project-base-dir (expand-file-name "."))
		 (build-dir
		  ;; (expand-file-name "~/Downloads/personal-website")
		  (expand-file-name "build" project-base-dir)
		  ))

	(setq-local org-publish-project-alist
				`(
				  (
				   "website-org-sources"
				   :base-directory ,(expand-file-name "org-sources"
													  project-base-dir)
				   :publishing-directory ,build-dir
				   ;; :preparation-function
				   ;; jan-website--prepare-publish-to-html
				   :publishing-function org-html-publish-to-html

				   :exclude "_\\.org$"
				   :recursive t

				   :auto-sitemap t
				   :sitemap-title "Sitemap"
				   :sitemap-format-entry jan-website--format-sitemap-entry
				   :sitemap-function jan-website--publish-sitemap-with-includes

				   :makeindex t
				   )

				  (
				   "website-content"
				   :components
				   (
					"website-org-sources"
					)
				   )

				  (
				   "website-css"
				   :base-directory ,(expand-file-name "css" project-base-dir)
				   :publishing-directory ,(expand-file-name "css" build-dir)
				   :publishing-function org-publish-attachment
				   :base-extension "css"
				   :exclude "_\\.css$"
				   )

				  (
				   "website-js"
				   :base-directory ,(expand-file-name "js" project-base-dir)
				   :publishing-directory ,(expand-file-name "js" build-dir)
				   :publishing-function org-publish-attachment
				   :base-extension "js"
				   :exclude "_\\.js$"
				   )

				  (
				   "website-favicon"
				   :base-directory ,project-base-dir
				   :publishing-directory ,build-dir
				   :publishing-function org-publish-attachment
				   :base-extension "svg\\|png"
				   )

				  (
				   "website-includes"
				   :components
				   (
					"website-css"
					"website-js"
					"website-favicon"
					)
				   )

				  (
				   "deployment-related"
				   :base-directory ,project-base-dir
				   :publishing-directory ,(expand-file-name build-dir)
				   :publishing-function org-publish-attachment
				   ;; Exclude everything usually added.
				   :exclude ".*\\.org$"
				   :include ("CNAME" ".nojekyll")
				   )

				  (
				   "personal-website"
				   :components
				   (
					"website-content"
					"website-includes"
					"deployment-related"
					)
				   )
				  ))))

;; Could be used if we disregarded prelude and instead put all settings in
;; the project alist. We could also load prelude here and keep the settings the
;; way they are now.
(defun jan-website--prepare-publish-to-html (&optional project-property-list)
  "Load important setup code for publishing.
PROJECT-PROPERTY-LIST is ignored."
  (let* ((include-parent-dir
		  (locate-dominating-file default-directory "includes"))
		 (include-dir (expand-file-name "includes" include-parent-dir))
		 (include-files '("prelude-elisp.org" "head.org"
						  "html-preamble.org" "html-postamble.org")))
	(mapc
	 (lambda (include-file)
	   (org-babel-load-file (expand-file-name include-file include-dir)))
	 include-files)))

(defun jan-website--format-sitemap-entry (entry style project)
  "Format ENTRY for the sitemap with STYLE in PROJECT.
The index page is formatted specially with the \"Home\" title. Other entries use
`org-publish-sitemap-default-entry'."
  (if (and (string= (file-name-nondirectory entry) "index.org")
		   (not (directory-name-p entry)))
	  (format "[[file:%s][Home]]" entry)
	(org-publish-sitemap-default-entry entry style project)))

(defun jan-website--publish-sitemap-with-includes (title list)
  "Publish a custom sitemap with TITLE, including project-relevant code.
The entries in LIST are formatted the usual way."
  (let ((relative-include-dir "../includes"))
	(concat "#+TITLE: " title "\n#+INDEX: " title
			"\n\n#+include: \"" relative-include-dir "/prelude.org\"\n"
			"#+include: \"" relative-include-dir "/my-room.org\"\n\n"
			(org-list-to-org list) "\n")))

(jan-website--setup-project)
#+end_src

# Completely clean publishment.
#+begin_src emacs-lisp :exports results :results none :var project-setup=setup-project :tangle yes
(require 'ox-publish)

;; Removing timestamps is especially important to get sitemap titles up to date.
(org-publish-remove-all-timestamps)
;; This is already called when removing timestamps; we do it again for
;; forward compatibility.
(org-publish-reset-cache)
(org-publish-project "personal-website" t)
#+end_src
