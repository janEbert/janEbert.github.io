#+begin_src emacs-lisp :exports results :results none
(require 'ox-html)

(message "loading head.org")

(defun jan-website--construct-head (relative-css-dir relative-favicon-dir)
  "Return the Org html head, linking to files in the given directories.
RELATIVE-CSS-DIR and RELATIVE-FAVICON-DIR are directory paths relative
to the published index.html."
  (jan-website--remove-indentation (concat "\
		<link href=\"" relative-css-dir "/nethack.css\" rel=\"stylesheet\">
		<link href=\"" relative-css-dir "/main.css\" rel=\"stylesheet\">
		<link rel=\"icon\" href=\"" relative-favicon-dir "/favicon.min.svg\">
		<link rel=\"alternate icon\" href=\""
		relative-favicon-dir "/favicon.png\">\
")))

(defun jan-website--construct-head-extra (relative-js-dir)
  "Return additions to Org html head, linking to files in RELATIVE-JS-DIR.
RELATIVE-JS-DIR is a directory path relative to the published index.html."
  (jan-website--remove-indentation (concat "\
		<script src=\"" relative-js-dir "/theme-switcher.js\" defer></script>

		<!--
			Avoid Chromium transition on page load bug
			https://bugs.chromium.org/p/chromium/issues/detail?id=167083
			https://bugs.chromium.org/p/chromium/issues/detail?id=332189
		-->

		<style>
			#title-name {
				font-size: 24px;
				display: inline-block;
				transition-duration: var(--title-name-transition-duration);
			}
		</style>

		<noscript>
			<style>
				#theme-togglers {
					display: none;
				}
			</style>
		</noscript>\
")))

;; TODO Check if dominating file is directory;
;; otherwise get parent of the directory containing the dominating file.
;; TODO We could use some error checks here whether the links are correct.

;; We walk up one directory before locating so the relative path is one
;; directory less deep. This way, the org source files can reside in their
;; own folder.
(let* ((parent-dir
		(file-name-directory (directory-file-name default-directory)))
	   (css-parent-dir (locate-dominating-file parent-dir "css"))
	   (relative-css-dir (file-relative-name
						  (expand-file-name "css" css-parent-dir)
						  parent-dir))
	   (js-parent-dir (locate-dominating-file parent-dir "js"))
	   (relative-js-dir (file-relative-name
						 (expand-file-name "js" js-parent-dir)
						 parent-dir))
	   (favicon-parent-dir (locate-dominating-file parent-dir "favicon.min.svg"))
	   (relative-favicon-dir
		(directory-file-name (file-relative-name
							  favicon-parent-dir parent-dir))))

  (setq-local org-html-head
			  (concat org-html-head
					  (jan-website--construct-head
					   relative-css-dir relative-favicon-dir)))

  (setq-local org-html-head-extra
			  (concat org-html-head-extra
					  (jan-website--construct-head-extra relative-js-dir))))
#+end_src
